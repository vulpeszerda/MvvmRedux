apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.novoda.bintray-release'

android {
    compileSdkVersion compileSdk
    buildToolsVersion buildTools

    lintOptions {
        quiet false
        abortOnError true
        checkReleaseBuilds true
        lintConfig file("../lint.xml")
    }
    defaultConfig {
        minSdkVersion minSdk
        targetSdkVersion targetSdk
        versionCode libraryVersionCode
        versionName libraryVersionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    packagingOptions {
        exclude "META-INF/DEPENDENCIES"
        exclude "META-INF/LICENSE"
        exclude "META-INF/LICENSE.txt"
        exclude "META-INF/NOTICE"
        exclude "META-INF/NOTICE.txt"
        exclude "META-INF/rxjava.properties"
        exclude "META-INF/maven/com.google.guava/guava/pom.properties"
        exclude "META-INF/maven/com.google.guava/guava/pom.xml"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        main.java.srcDirs += [file("$buildDir/generated/source/kapt/main")]
        test.java.srcDirs += 'src/test/kotlin'
        test.java.srcDirs += [file("$buildDir/generated/source/kapt/test")]
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        androidTest.java.srcDirs += [file("$buildDir/generated/source/kapt/androidTest")]
    }
}

androidExtensions {
    experimental = true
}

kapt {
    generateStubs = true
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    testImplementation "junit:junit:$junit"

    implementation "com.android.support:support-v4:$supportLib"
    implementation "com.android.support:appcompat-v7:$supportLib"
    androidTestImplementation "com.android.support:support-v4:$supportLib"
    androidTestImplementation "com.android.support:appcompat-v7:$supportLib"

    /* RxJava related */
    implementation "io.reactivex.rxjava2:rxjava:$rxjava2"
    implementation "io.reactivex.rxjava2:rxkotlin:$rxkotlin2"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid2"
    implementation "com.jakewharton.rxbinding2:rxbinding:$rxbinding2"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4:$rxbinding2"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7:$rxbinding2"
    implementation "com.trello.rxlifecycle2:rxlifecycle:$rxlifecycle2"
    implementation "com.trello.rxlifecycle2:rxlifecycle-android:$rxlifecycle2"
    implementation "com.trello.rxlifecycle2:rxlifecycle-android-lifecycle:$rxlifecycle2"
    implementation "com.trello.rxlifecycle2:rxlifecycle-kotlin:$rxlifecycle2"
    implementation "com.trello.rxlifecycle2:rxlifecycle-android-lifecycle-kotlin:$rxlifecycle2"


    implementation "android.arch.lifecycle:runtime:$androidArch"
    implementation "android.arch.lifecycle:extensions:$androidArch"
    kapt "android.arch.lifecycle:compiler:$androidArch"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin"
}

publish {
    groupId = 'com.vulpeszerda.mvvmredux'
    artifactId = 'mvvmredux'
    publishVersion = libraryVersionName
    desc = 'Android mvvm using android arch component'
    website = 'https://github.com/vulpeszerda/MvvmRedux'
    bintrayUser = project.hasProperty("bintrayUser") ? bintrayUser : ""
    bintrayKey = project.hasProperty("bintrayKey") ? bintrayKey : ""
}
