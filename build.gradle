// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        kotlin = "1.2.41"
        androidPlugin = "3.1.2"
        dexcount = "0.6.3"
        bintray = "0.8.0"
    }
    repositories {
        jcenter()
        mavenCentral()
        google()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$androidPlugin"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin"
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:$dexcount"
        classpath "com.novoda:bintray-release:$bintray"
    }
}

allprojects {
    project.ext {
        kotlin = kotlin

        compileSdk = 27
        targetSdk = 27
        minSdk = 16

        supportLib = "27.1.1"
        constraintLayout = "1.1.0"

        junit = "4.12"
        espresso = "3.0.0"

        rxJava2 = "2.1.12"
        rxAndroid2 = "2.0.2"
        rxKotlin2 = "2.2.0"
        rxBinding2 = "2.1.1"
        rxLifecycle2 = "2.2.1"
        rxPermission2 = "0.9.5@aar"

        androidArch = "1.1.1"
        room = "1.0.0"

        sampleVersionCode = 2
        sampleVersionName = "1.1.0"

        libraryVersionCode = 2
        libraryVersionName = "1.2.0"

        java7Home = project.hasProperty("java7_home") ?
                project.property("java7_home") : System.getenv("JAVA7_HOME")
        java8Home = project.hasProperty("java8_home") ?
                project.property("java8_home") : System.getenv("JAVA8_HOME")
    }
    repositories {
        jcenter()
        mavenCentral()
        google()
        maven { url "https://jitpack.io" }
    }

    tasks.withType(Javadoc).all {
        enabled = false
    }
}

project.ext.preDexLibs = !project.hasProperty('disablePreDex')

subprojects {
    project.plugins.whenPluginAdded { plugin ->
        if ("com.android.build.gradle.AppPlugin" == plugin.class.name) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        } else if ("com.android.build.gradle.LibraryPlugin" == plugin.class.name) {
            project.android.dexOptions.preDexLibraries = rootProject.ext.preDexLibs
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
